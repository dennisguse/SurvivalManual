apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.trevjonez.composer'

android {
    compileSdk 34

    namespace "org.ligi.survivalmanual"

    defaultConfig {
        applicationId "org.ligi.survivalmanual"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 428
        versionName "4.2.8"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "org.ligi.trulesk.BaseTruleskRunner"
        archivesBaseName = "SurvivalManual-$versionName"
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "store"
    productFlavors {
        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/*'
    }

    lintOptions {
        warning 'InvalidPackage'
        warning 'MissingTranslation'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

// Android support
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.android.material:material:1.11.0'

// Third party
    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"

    implementation 'org.jetbrains:markdown:0.6.1'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.6'
    implementation 'com.github.ligi:ExtraCompats:1.0'
    implementation 'com.github.ligi:tracedroid:4.1'
    implementation 'com.github.ligi:KAXT:1.0'

    forPlayImplementation "com.github.ligi.snackengage:snackengage-playrate:$snackengage_version"
    forFDroidImplementation "com.github.ligi.snackengage:snackengage-playrate:$snackengage_version"
    forAmazonImplementation "com.github.ligi.snackengage:snackengage-amazonrate:$snackengage_version"


// Testing
    androidTestImplementation 'androidx.appcompat:appcompat:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"

    androidTestImplementation 'com.github.ligi:trulesk:4.3'
    androidTestImplementation 'org.mockito:mockito-core:5.10.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.3'

    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.annotation:annotation:1.7.1'
}

composer {
    apkInstallTimeout 600
}
